SakuraMenuGreetings : nonoverlap
{
	"%(MRneut)Little souls, little souls."
	"%(MRneut)What a %(positive) day."
	"%(MRneut)Hello? Is newnew soul there?"
	"%(MRneut)Cosmos speak in names of souls and lives once lived."
	"%(MRneut)Over and over and over again. The eternal%(positive) cycle."
	if goretalk == 1{
		"%(MRmelan)Skin flaking, bones breaking, for a %(negative) death."
		"%(MRneut)A life of bleeding and death in boiling oil."
	}
}

//Because there is an all modifier here, you don't need to write -- between menu options
OnSakuraMenu : all
{
	"\C\_q\![lock,balloonrepaint]\c\![unlock,balloonrepaint]" //deflickerer
	"\f[height,%textsize]\1\s[-1]\0\b2\![set,autoscroll,disable]\n\n"
	
	"\n\n"
	
	"\![*]\__q[OnAiTalk]Talk to yourself\__q"
	if LastTalk != ""{
		if textsize >=22{
			"\n\![*]\__q[OnLastTalk]Repeat yourself\__q"
		}else{
			"  \![*]\__q[OnLastTalk]Repeat yourself\__q"
		}
	}
	"\n\![*]\__q[OnMaze]Do Some Soul Searching\__q (Souls: %souls)"
	"\n\n[half]"
	
	"\![*]\__q[OnSettingsMenu]Settings\__q\n\n"
	
	"\![*]\__q[blank]Nevermind\__q"	
	
	if SHIORI3FW.Eventid == "OnMouseDoubleClick"; "\_q\_l[0,0]%(SakuraMenuGreetings)" //The check here is so they won't comment if you click the back button. Remove if you prefer
	
}

OnLastTalk
{
	LastTalk
}

OnSettingsMenu : all
{	
	//This is just setting up a label, you may need to add to it or tweak it if you change what rates are available
	_talkrate = "Off"
	if aitalkinterval == 60; _talkrate = "Every minute"
	elseif aitalkinterval == 180; _talkrate = "3 minutes"
	elseif aitalkinterval == 300; _talkrate = "5 minutes"
	elseif aitalkinterval == 600; _talkrate = "10 minutes"
	
	"%(MRneut)\b2\![set,autoscroll,disable]\_q"
	"Name: \![*]\q[%(nickname) ,OnNameChange]\n\n[half]"
	
	//Text size for Acessibility
	"Change text size: \q[ -  ,OnDownscaleText]%(textsize)\q[  + ,OnUpscaleText]\n\n[half]"
	
	//Gory dialogue
	if goretalk == 1{
		"Gore topics: \![*]\q[On,OnFlipGore]"
	}else{
		"Gore topics: \![*]\q[Off,OnFlipGore]"
	}
	
	"\n\n[half]Current talk interval: %(_talkrate)\n\n[half]"
	
	"\![*]\q[Off,OnTalkRateChange,0]  "
	"\![*]\q[1m,OnTalkRateChange,60]  "
	"\![*]\q[3m,OnTalkRateChange,180]  "
	"\![*]\q[5m,OnTalkRateChange,300]  "
	"\![*]\q[10m,OnTalkRateChange,600]  "
	"\n\n"
	"\![*]\q[Back,OnSakuraMenu]  \![*]\q[Nevermind,blank]"
}

OnTalkRateChange
{
	aitalkinterval = TOINT(reference0)
	"%(OnSettingsMenu)"
}

OnUpscaleText{
	textsize++
	OnSettingsMenu
}

OnDownscaleText{
	textsize--
	OnSettingsMenu
}

OnFlipGore{
	goretalk = !goretalk
	"%(OnSettingsMenu)"
}


OnNameChange{
	"\![open,inputbox,OnNameChangeSuccess,0,Weary Soul]"
}

OnNameChangeSuccess{
	nickname = reference0
	"%(OnSettingsMenu)"

}